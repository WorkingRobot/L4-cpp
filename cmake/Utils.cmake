include_guard(GLOBAL)

macro(L4_hoist_variable VARIABLE)
    set(${VARIABLE} ${${VARIABLE}} PARENT_SCOPE)
endmacro()

function(L4_dump_variables)
    get_cmake_property(_vars VARIABLES)
    list(SORT _vars)
    foreach(_var ${_vars})
        message(STATUS "${_var}=${${_var}}")
    endforeach()
endfunction()

function(L4_add_subdirectories_in PATH)
    file(GLOB CHILD_LIST ${PATH}/*)
    foreach(CHILD_PATH ${CHILD_LIST})
        if(IS_DIRECTORY ${CHILD_PATH})
            list(APPEND DIR_LIST ${CHILD_PATH})
        endif()
    endforeach()
    list(REMOVE_DUPLICATES DIR_LIST)

    foreach(DIR_PATH ${DIR_LIST})
        add_subdirectory(${DIR_PATH})
    endforeach()
endfunction()

function(L4_get_platform_identifier VARIABLE)
    # Taken from https://github.com/microsoft/vcpkg/blob/9259a0719d94c402aae2ab7975bc096afdec15df/scripts/buildsystems/vcpkg.cmake#L332
    if (NOT CMAKE_SYSTEM_NAME)
        set(SYSTEM_NAME ${CMAKE_HOST_SYSTEM_NAME})
    else()
        set(SYSTEM_NAME ${CMAKE_SYSTEM_NAME})
    endif()
    if (SYSTEM_NAME STREQUAL "Windows")
        set(${VARIABLE} "win")
    elseif (SYSTEM_NAME STREQUAL "Linux")
        set(${VARIABLE} "lnx")
    elseif (SYSTEM_NAME STREQUAL "Darwin")
        set(${VARIABLE} "osx")
    else()
        message(FATAL_ERROR "Unsupported platform: ${SYSTEM_NAME}")
    endif()
    L4_hoist_variable(${VARIABLE})
endfunction()

L4_get_platform_identifier(PROJECT_PLATFORM)
